#!/usr/bin/gnuplot

fileName="weatherBandit.png"
bannerFileName="overlay-graph.png"
pathToFile="/var/www/html/img"
#load ".config"
system("./whlog2csv")

set datafile separator ","
set term pngcairo dashed
set output pathToFile."/".fileName
set style line 1
set timefmt "%s"
set xdata time
set xlabel "Time"
set xtics 43200 rotate 

set timestamp
set title "Weatherbandit data logger"
set ylabel "Temperature C"
set y2label "Humidity %"
#set y2range [5:100]
set y2tics 10 nomirror 
plot for [ch=1:3] '<grep ,'.ch.', wh.csv' using (timecolumn(1)-8*3600):3 with line lc ch lw 2 title 'Ch '.ch, \
     for [ch=1:3] '<grep ,'.ch.', wh.csv' using (timecolumn(1)-8*3600):4 with line dt 2 lc ch title '' axes x1y2


set output pathToFile."/".bannerFileName
set term pngcairo dashed transparent size 900,200
unset title
unset xlabel 
plot for [ch=1:3] '<grep ,'.ch.', wh.csv' using 1:3 with line lc ch lw 2 title 'Ch '.ch, \
     for [ch=1:3] '<grep ,'.ch.', wh.csv' using 1:4 with line dt 2 lc ch title '' axes x1y2 
